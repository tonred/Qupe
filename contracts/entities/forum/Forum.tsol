pragma ever-solidity ^0.66.0;

import "../../structures/ForumUserData.tsol";
import "../../structures/Infos.tsol";
import "../abstract/Server.tsol";


contract Forum is Server {
    using PaymentLibrary for Payment;

    ForumInfo _info;
    TvmCell _messageCode;


    function _init(TvmCell params) internal override returns (
        address owner, address token, optional(Permissions) ownerPermissions
    ) {
        (owner, _info, _roomCode, _messageCode) = abi.decode(params, (address, ForumInfo, TvmCell, TvmCell));
        token = _info.createRoomPayment.token;
        ownerPermissions = PermissionsLibrary.full();
        Permissions defaultPermissions = PermissionsLibrary.single(Action.CREATE_ROOM);
        _initPermissions(defaultPermissions);
    }

    function changeInfo(CallData data, ForumInfo info) public cashBack hasPermission(data, Action.CONFIG) {
        _info = info;
    }

    function _checkCreateRoomData(CallData data) internal view override returns (bool) {
        Payment price = _info.createRoomPayment;
        if (!data.payment.moreThan(price)) {
            return false;
        }
        if (!data.userData.hasValue()) {
            return false;
        }
        ForumUserData userData = abi.decode(data.userData.get(), ForumUserData);
        return userData.reputation >= _info.createRoomReputation;
    }

    function _buildCreateRoomInitialParams(TvmCell params, address /*creator*/) internal view override returns (TvmCell) {
        (address owner, TopicInfo info) = abi.decode(params, (address, TopicInfo));
        return abi.encode(owner, info, _messageCode);
    }


    function _encodeContractData() internal override returns (TvmCell) {
        return abi.encode(
            _token, _wallet, _balance,                          // Vault
            _root, _platformCode,                               // PlatformUtils
            _sid, _version,                                     // BaseSlave
            _defaultPermissions,                                // Base
            _serverID, _roomCode, _roomsCount, _usersCount,     // Server
            _info, _messageCode
        );
    }

}
