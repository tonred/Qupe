pragma ever-solidity ^0.66.0;

import "../../structures/ForumUserData.tsol";
import "../../structures/Infos.tsol";
import "../abstract/Room.tsol";


contract Topic is Room {

    TopicInfo _info;
    TvmCell _messageCode;


    function _init(TvmCell params) internal override returns (address owner, address token) {
        (owner, _info, _messageCode) = abi.decode(params, (address, TopicInfo, TvmCell));
        Permissions defaultPermissions = PermissionsLibrary.single(Action.SEND_MESSAGE);
        _initPermissions(owner, defaultPermissions);
        token = PaymentLibrary.jointToken(_info.messagePayment, _info.highlightMessagePayment);
    }

    function changeInfo(CallData data, TopicInfo info) public cashBack hasPermission(data, Action.CONFIG) {
        _info = info;
    }


    function _checkAcceptMessageData(CallData data, bool highlight) internal view override returns (bool) {
        Payment price = highlight ? _info.highlightMessagePayment : _info.messagePayment;
        if (!data.payment.moreThan(price)) {
            return false;
        }
        if (!data.userData.hasValue()) {
            return false;
        }
        ForumUserData userData = abi.decode(data.userData.get(), ForumUserData);
        return userData.reputation >= _info.messageReputation;
    }

    function _acceptMessage(CallData data, uint messageHash, Tag[] tags, bool highlight) internal override {
        uint64 messageID = _acceptMessageBase(data, messageHash, tags, highlight);
        TvmCell stateInit = _buildMessageStateInit(_serverID, _roomID, messageID);
        TvmCell params = abi.encode(messageHash, data.user);
        new Platform{
            stateInit: stateInit,
            value: Gas.DEPLOY_MESSAGE_VALUE,
            flag: MsgFlag.SENDER_PAYS_FEES,
            bounce: false
        }(_messageCode, params);
        msg.sender.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

}
