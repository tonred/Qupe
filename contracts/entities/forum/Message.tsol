pragma ever-solidity ^0.66.0;

import "../../platform/PlatformUtils.tsol";
import "../../utils/Gas.tsol";
import "../abstract/interfaces/IProfile.tsol";
import "./interfaces/IForumProfile.tsol";
import "./interfaces/IForumUser.tsol";

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";


contract Message is PlatformUtils {

    uint64 _serverID;
    uint64 _roomID;
    uint64 _messageID;

    uint _messageHash;
    address _owner;
    mapping(uint32 /*reaction*/ => uint64 /*count*/) _reactions;
    int64 _reputation;


    modifier onlyProfile(address user) {
        address profile = _profileAddress(user);
        require(msg.sender == profile, ErrorCodes.SENDER_IS_NOT_PROFILE);
        _;
    }

    function onCodeUpgrade(address root, TvmCell platformCode, TvmCell initialData, TvmCell initialParams) internal {
        tvm.resetStorage();
        _reserveTarget();
        _initPlatform(root, platformCode);
        (_serverID, _roomID, _messageID) = abi.decode(initialData, (uint64, uint64, uint64));
        (_messageHash, _owner) = abi.decode(initialParams, (uint, address));
        address profile = _profileAddress(_owner);
        IProfile(profile).onMessageAccepted{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_serverID, _roomID, _messageID, _messageHash, true);
    }


    function getIDs() public view responsible returns (uint64 serverID, uint64 roomID, uint64 messageID) {
        return {value: 0, flag: MsgFlag.SENDER_PAYS_FEES, bounce: false} (_serverID, _roomID, _messageID);
    }

    function getDetails() public view responsible returns (uint messageHash, address owner) {
        return {value: 0, flag: MsgFlag.SENDER_PAYS_FEES, bounce: false} (_messageHash, _owner);
    }

    function getReactions() public view responsible returns (mapping(uint32 => uint64) reactions) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} _reactions;
    }

    function getReactionCount(uint32 reaction) public view responsible returns (uint64 count) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} _reactions[reaction];
    }

    function getReputation() public view responsible returns (int64 reputation) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} _reputation;
    }


    function react(address user, uint32 reaction, bool undo) public onlyProfile(user) {
        _reserveTarget();
        undo ? _reactions[reaction]-- : _reactions[reaction]++;
        IForumUser(user).onReact{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(reaction, undo);
    }

    function changeReputation(address user, int8 change) public onlyProfile(user) {
        _reserveTarget();
        _reputation += change;
        address profile = _profileAddress(user);
        IForumProfile(profile).onReputationChanged{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_serverID, _roomID, _messageID, user, change);
    }

    function _reserveTarget() internal pure {
        tvm.rawReserve(Gas.MESSAGE_TARGET_BALANCE, 0);
    }

}
