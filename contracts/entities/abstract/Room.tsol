pragma ever-solidity ^0.66.0;

import "../../payment/PaymentLibrary.tsol";
import "../../structures/Tag.tsol";
import "./interfaces/IProfile.tsol";
import "./Base.tsol";


abstract contract Room is Base {
    using PaymentLibrary for Payment;

    event MessageAccepted(uint64 messageID, uint messageHash, bool highlight);
    event Banned(address user);
    event Unbanned(address user);

    uint64 _serverID;
    uint64 _roomID;

    mapping(address => bool) _bans;
    uint64 _messagesCount;


    function onCodeUpgrade(address root, TvmCell platformCode, TvmCell initialData, TvmCell initialParams) internal {
        tvm.resetStorage();
        _initPlatform(root, platformCode);
        (_serverID, _roomID) = abi.decode(initialData, (uint64, uint64));
        (address owner, address token) = _init(initialParams);
        _initVault(token);

        _reserveTarget();
        IUser(owner).onRoomCreated{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_serverID, _roomID);
    }

    function _init(TvmCell params) internal virtual override returns (address owner, address token);


    function acceptMessage(
        CallData data,
        uint messageHash,
        Tag[] tags,
        bool highlight
    ) public hasPermission(data, Action.SEND_MESSAGE) {
        require(_checkAcceptMessageData(data, highlight), ErrorCodes.INVALID_MESSAGE_DATA);
        _acceptMessage(data, messageHash, tags, highlight);
    }

    function ban(CallData data, address user) public hasPermission(data, Action.BAN_UNBAN) {
        _reserve();
        emit Banned(user);
        _setPermissions(user, PermissionsLibrary.none(), data.user);
        IUser(user).onBan{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(_serverID, _roomID);
    }

    function unban(CallData data, address user) public hasPermission(data, Action.BAN_UNBAN) {
        _reserve();
        emit Unbanned(user);
        _setPermissions(user, null, data.user);
        IUser(user).onUnban{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(_serverID, _roomID);
    }

    // todo manageRoom
//    function changeParams(
//        uint64 serverID,
//        TvmCell params,
//        bool resetPermissions,
//        address remainingGasTo
//    ) public onlyServer(serverID) {
//        _reserve();
//        address owner = _init(params);
//        if (resetPermissions) {
//            _resetPermissions(owner);
//        }
//        remainingGasTo.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
//    }


    function _onAcceptTokensTransfer(uint128 amount, address sender, TvmCell payload) internal override {
        (CallData data, uint messageHash, Tag[] tags, bool highlight) =
            abi.decode(payload, (CallData, uint, Tag[], bool));
        bool correct_sender = _checkSender(sender, data.user);
        bool has_permission = _checkPermissions(data, Action.SEND_MESSAGE);
        bool correct_data = _checkAcceptMessageData(data, highlight);
        if (!correct_sender || !has_permission || !correct_data) {
            _returnTokens(amount, sender);
            return;
        }
        _acceptMessage(data, messageHash, tags, highlight);
    }

    function _checkAcceptMessageData(CallData data, bool highlight) internal view virtual returns (bool);

    function _acceptMessage(CallData data, uint messageHash, Tag[] tags, bool highlight) internal virtual {
        uint64 messageID = _acceptMessageBase(data, messageHash, tags, highlight);
        IProfile(msg.sender).onMessageAccepted{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_serverID, _roomID, messageID, messageHash, false);
    }

    function _acceptMessageBase(CallData data, uint messageHash, Tag[] tags, bool highlight) internal returns (uint64) {
        uint128 additional = Gas.ACCEPT_MESSAGE_FEE + data.payment.evers();
        tvm.rawReserve(additional, 4);
        uint64 messageID = _messagesCount++;
        emit MessageAccepted(messageID, messageHash, highlight);
        for (Tag tag : tags) {
            address profile = _profileAddress(tag.user);
            IProfile(profile).onTagged{
                value: tag.value,
                flag: MsgFlag.SENDER_PAYS_FEES,
                bounce: true
            }(_serverID, _roomID, messageID, data.user);
        }
        return messageID;
    }

    function _setPermissions(address user, optional(Permissions) permissions, address callbackTo) internal override {
        address profile = _profileAddress(user);
        IProfile(profile).setRoomPermissions{
            value: Gas.SET_PERMISSION_VALUE,
            flag: MsgFlag.SENDER_PAYS_FEES,
            bounce: false
        }(_serverID, _roomID, permissions, callbackTo);
    }

    function _reserveTarget() internal view inline override {
        tvm.rawReserve(Gas.ROOM_TARGET_BALANCE, 0);
    }

}
