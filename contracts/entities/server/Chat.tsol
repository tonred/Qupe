pragma ever-solidity ^0.66.0;

pragma AbiHeader time;
pragma AbiHeader expire;
pragma AbiHeader pubkey;
pragma experimental ABIEncoderV2;

import "../abstract/Server.tsol";
import "../../structures/Infos.tsol";


contract Chat is Server {

    ChatInfo public _info;


    function _init(TvmCell params) internal override {
        address owner;
        mapping(uint8 => bool) defaultRights;
        (owner, _info, _roomCode) = abi.decode(params, (address, ChatInfo, TvmCell));
        defaultRights[uint8(Action.CREATE_ROOM)] = true;
        _initPermissions(owner, defaultRights);
    }

    function changeInfo(ChatInfo info) public cashBack hasPermission(Action.CONFIG) {
        _info = info;
    }

    function encodeCreateRoomParams(address owner, ChatInfo info) public pure responsible returns (TvmCell params) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} abi.encode(owner, info);
    }


    function _checkCreateRoomParams(TvmCell params) internal pure override returns (bool) {
        try {
            abi.decode(params, (address, ChatInfo));
            return true;
        } catch (variant /*value*/, uint /*errorCode*/) {}
        return false;
    }

    function _checkCreateRoomData(Payment payment, optional(UserData) /*data*/) internal view override returns (bool) {
        Payment expected = _info.createRoomPayment;
        return _checkPayment(payment, expected);
    }

    function _buildCreateRoomInitialParams(TvmCell params, address /*creator*/) internal view override returns (TvmCell) {
        return params;
    }

}
